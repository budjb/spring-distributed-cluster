dependencyManagement {
    imports { mavenBom("org.springframework.boot:spring-boot-dependencies:${springBootVersion}") }
}

dependencies {
    compile 'com.budjb:spring-distributed-locks:0.1.4.BETA'
    compile 'org.springframework.boot:spring-boot-starter'
    compile 'org.springframework.boot:spring-boot-starter-validation'

    testCompile 'org.spockframework:spock-core:1.1-groovy-2.4'
    testCompile 'cglib:cglib-nodep:3.2.6'
    testCompile 'org.objenesis:objenesis:2.6'

    optional "org.springframework.boot:spring-boot-configuration-processor"
}

publishing {
    publications {
        library(MavenPublication) {
            from components.java

            artifact sourcesJar
            artifact javadocJar

            groupId project.group
            artifactId project.name
            version project.version

            pom.withXml {
                asNode().children().last() + {
                    resolveStrategy = DELEGATE_FIRST

                    name project.name
                    description projectDescription
                    url githubHttpsUrl
                    packaging 'jar'

                    licenses {
                        license {
                            name 'The Apache License, Version 2.0'
                            url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                        }
                    }
                    developers {
                        developer {
                            id 'budjb'
                            name 'Bud Byrd'
                            email 'bud.byrd@gmail.com'
                        }
                    }
                    scm {
                        url githubHttpsUrl
                        connection "scm:git:${githubHttpsUrl}.git"
                        developerConnection "scm:git:${githubGitUrl}"
                    }
                }
            }
        }
    }
}

bintray {
    user = System.getenv(bintrayUsernameEnvVar)
    key = System.getenv(bintrayKeyEnvVar)
    publish = true

    pkg {
        websiteUrl = githubHttpsUrl
        issueTrackerUrl = "${githubHttpsUrl}/issues"
        vcsUrl = githubGitUrl
        githubRepo = 'budjb/spring-distributed-cluster'

        desc = projectDescription
        labels = ['spring-boot', 'spring-framework']
        repo = 'spring'
        name = project.name
        licenses = ['Apache-2.0']
        publications = ['library']

        version {
            name = project.version
            released = new Date()
            vcsTag = project.version
            gpg {
                sign = true
                passphrase = System.getenv('GPG_PASSWORD')
            }
        }
    }
}
